# vim: syntax=zsh

################################################################
# Antigen
################################################################

  source "$HOME/.zsh/antigen/antigen.zsh"

  # Load the oh-my-zsh's library.
  antigen use oh-my-zsh

  # Bundles from the default repo (robbyrussell's oh-my-zsh).
  antigen bundle command-not-found  # suggests package name with relevant command
  antigen bundle docker
  antigen bundle git-flow-avh
  antigen bundle pip

  # Bundles from other repos
  antigen bundle zsh-users/zsh-completions src  # Additional completion definitions for Zsh
  antigen bundle sdurrheimer/docker-compose-zsh-completion

  # Local repos
  antigen bundle "$HOME/.zsh/completions" --no-local-clone

  # Load the theme.
  DEFAULT_USER="$USER"
  POWERLEVEL9K_LEFT_PROMPT_ELEMENTS=(dir dir_writable vcs)
  POWERLEVEL9K_RIGHT_PROMPT_ELEMENTS=(background_jobs direnv virtualenv time)
  GITSTATUS_DAEMON=/usr/share/gitstatus/usrbin/gitstatusd
  antigen theme romkatv/powerlevel10k

  # Tell antigen that you're done.
  antigen apply

################################################################
# Set ZSH options
################################################################
#== History ====================
  setopt APPEND_HISTORY          # history appends to existing file
  setopt HIST_FIND_NO_DUPS       # history search finds once only
  setopt HIST_IGNORE_ALL_DUPS    # remove all earlier duplicate lines
  setopt HIST_REDUCE_BLANKS      # trim multiple insgnificant blanks in history
  setopt HIST_NO_STORE           # remove the history (fc -l) command from the history when invoked

  HISTFILE="$HOME/.zsh/history"    # history file location
  HISTSIZE=1000000               # number of history lines kept internally
  SAVEHIST=1000000               # max number of history lines saved

################################################################
# Functions
################################################################
#== Builtin ====================

# calculator
# docs: https://github.com/zsh-users/zsh/blob/master/Functions/Misc/zcalc
autoload zcalc

# bulk rename / copy / make links
# docs: https://github.com/zsh-users/zsh/blob/master/Functions/Misc/zmv
autoload zmv

#== Custom =====================

# Decrypt EncFS
  function encbox() {
    if ! (type encfs &> /dev/null); then
      echo "Please install encfs: sudo pacman -Sy encfs"
      return
    fi
    local src="${1:-$HOME/cloud/.encrypted}"
    local target="${2:-$HOME/encfs/cloud}"
    [[ ! "$(ls -A $target)" ]] && encfs "$src" "$target"
  }

# Grepping history
  function hsi() {
    history | grep -i $1
  }

# Mount / unmount usb flash
  function pmount() {
    if ! (type udisksctl &> /dev/null); then
      echo "Please install udisksctl: sudo pacman -Sy udisksctl"
      return
    fi
    local device="/dev/$1"
    local target="$(findmnt --output TARGET $device | sed -n 2p)"
    if [[ "$target" ]]; then
      echo "Alredy mounted to: $target"
      return
    fi
    udisksctl mount -b "$device"
  }

  function pumount() {
    if ! (type udisksctl &> /dev/null); then
      echo "Please install udisksctl: sudo pacman -Sy udisksctl"
      return
    fi
    udisksctl unmount -b "/dev/$1"
  }

# Extract archives with 7z
  function x() {
    if ! (type 7z &> /dev/null); then
      echo "Please install 7zip: sudo pacman -Sy p7zip"
      return
    fi
    local archive="$1"
    local output="${archive%.*}"
    7z x "$archive" -o"${2:-$output}"
  }

# Download MP3 audio with YouTube-DL
  function youtube-dl-audio() {
    if ! (type youtube-dl &> /dev/null); then
      echo "Please install YT-DLP: sudo pacman -Sy yt-dlp"
      return
    fi
    yt-dlp -f "bestaudio/best" -ciw -o "%(title)s.%(ext)s" -v --extract-audio --audio-format mp3 --audio-quality 0 --embed-thumbnail --yes-playlist "$1"
  }

################################################################
# Aliases
################################################################

  alias :q='exit'

  ccat="$GOPATH/bin/ccat"
  [[ -x $ccat ]] && alias cat="$ccat --color=always"

  alias todo='todo.sh'

  alias zcp="zmv -C"
  alias zln="zmv -L"

################################################################
# Autostart
################################################################
# Run 'ssh-agent' and preserve only one instance
if ! pgrep -u "$USER" ssh-agent > /dev/null; then
    ssh-agent > "$XDG_RUNTIME_DIR/ssh-agent.env"
fi
if [[ ! "$SSH_AUTH_SOCK" ]]; then
    source "$XDG_RUNTIME_DIR/ssh-agent.env" >/dev/null
fi

# Hook direnv in shell
# https://github.com/direnv/direnv
export DIRENV_LOG_FORMAT=
eval "$(direnv hook zsh)"
